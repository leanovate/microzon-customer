import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.5.RELEASE")
    }
}

plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'

group = "de.leanovate.microzon"
version =  '0.1.0'

jar {
    baseName = 'customer'
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-data-rest:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-actuator:1.2.1.RELEASE")
    compile("mysql:mysql-connector-java:5.1.32")
    compile("com.github.kristofa:brave-impl:2.2.1") {
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compile("com.github.kristofa:brave-zipkin-spancollector:2.2.1") {
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

task dist(type:Copy) {
    def distDir = System.getenv("DIST_DIR") ?: "../vagrant/dists"

    dependsOn build
    from jar.archivePath
    into distDir
}

task "dist-docker"(type:Copy) {
    def distDir = System.getenv("DIST_DIR") ?: "../docker/customer/dist"

    dependsOn build
    from jar.archivePath
    into distDir
}

task debian(dependsOn: build) << {
    copy {
        from 'src/main/debian'
        into 'build/deb/DEBIAN'
        filter(ReplaceTokens, tokens: [version: "$version-${System.env.TRAVIS_BUILD_NUMBER}".toString()])
    }
    copy {
        from jar.archivePath
        into 'build/deb/opt/customer'
        rename(/customer.*.jar/, 'customer.jar')
    }
    exec {
        executable '/usr/bin/dpkg-deb'
        args "-b", "build/deb", 'build/microzon-customer.deb'
    }
}

model {
    tasks.bintrayUpload {
        dependsOn debian
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "customer"
            artifact jar.archivePath
        }
    }
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_KEY"
    filesSpec {
        from 'build/microzon-customer.deb'
        into 'deps'
    }
    dryRun = false
    publish = true
    pkg {
        repo = 'deb'
        name = 'microzon'
    }
}